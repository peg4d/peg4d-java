TopLevel
	= PROLOG? _* DTD? _* Element WS*
	;
PROLOG
	= '<?xml' (!'?>' .)* '?>'
	;
_
	= [ \t\r\n]
	;
DTD
	= '<!' (!'>' .)* '>'
	;
Element
	= <{ '<' Name^ (_+ Attribute^)* _* ( '/>' / '>' _* Content '</' NAME '>' ) #xml }> _*
	;
Name
	= <{ NAME #name }>
	;
NAME    = [A-Za-z:] ('-' / [A-Za-z0-9:._])*;

Attribute
	= <{ Name^ _* '=' _* String^ #attr }>
	;
String
	= '"' <{ (!'"' .)* #string }> '"'
	;
Content
	= (Element^ / CDataSec^ / COMMENT / Text^)*
	;
CDataSec
	= '<![CDATA[' <{ (!']]>' .)* #cdata }> ']]>' _*
	;
CDataSec2
        = '<![CDATA[' <{ (!']]>' '<![CDATA[' .)* CDATA? #cdata }> ']]>' _*
        ;
CDATA
	= '<![CDATA[' (!']]>' '<![CDATA[' .)* CDATA? ']]>'
	;
COMMENT
	= '<!--' (!'-->' .)* '-->' _*
	;
Text
	= <{ (!'<' .)+ #cdata }> / ''
	;
WS
	= COMMENT
	/ _+
	;
